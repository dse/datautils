#!/usr/bin/env perl
use warnings;
use strict;
use File::Basename qw(basename);
our $PROGNAME = basename($0);

use open OUT => qw(:locale);

my $optSort = 0;
my $optColor = '';              # same as 'auto'
use Getopt::Long;
Getopt::Long::Configure('bundling', 'gnu_compat', 'no_ignore_case');
Getopt::Long::GetOptions(
    's|sort' => \$optSort,
    'color:s' => \$optColor,
) or die("You can't type '$PROGNAME --help' for help.\n");

use JSON;
my $json = JSON->new()->pretty(1)->canonical(1)->ascii(1);

my $isMultiple = scalar @ARGV >= 2;

my $GREEN = "\e[0;32m";
my $RESET = "\e[0m";
if ($optColor eq 'always') {
    # do nothing
} elsif ($optColor eq '' || $optColor eq 'auto') {
    if (!-t 1) {
        $GREEN = '';
        $RESET = '';
    }
} elsif ($optColor eq 'never') {
    $GREEN = '';
    $RESET = '';
} else {
    die("$PROGNAME: invalid --color value: $optColor\n");
}

$/ = undef;
while (<>) {
    # seen in some JSON output by `mongoexport --jsonArray`.
    s{":\+Infinity\b}{":"<<<+Infinity>>>"}g;
    s{":\-Infinity\b}{":"<<<-Infinity>>>"}g;

    warn("$PROGNAME: parsing $ARGV ...\n");
    my $object = $json->decode($_);

    warn("$PROGNAME: counting objects in $ARGV ...\n");
    my $counts = count($object);

    if ($isMultiple) {
        print("${GREEN}>>> ${ARGV} <<<${RESET}\n");
    }
    printCounts($counts);
}

sub count {
    my ($obj, $counts, $key) = @_;
    $counts //= {};
    $key //= '<root>';

    if (ref $obj eq '') {
        $counts->{$key} += 1;
    }
    if (ref $obj eq 'ARRAY') {
        foreach my $o (@$obj) {
            count($o, $counts, $key);
        }
    }
    if (ref $obj eq 'HASH') {
        foreach my $k (keys %$obj) {
            if (ref $obj->{$k} eq 'HASH') {
                my $kounts = $counts->{'{}'} //= {};
                count($obj->{$k}, $kounts->{$k} //= {}, $k);
            } else {
                count($obj->{$k}, $counts, $k);
            }
        }
    }
    return $counts;
}

sub printCounts {
    my ($counts) = @_;
    print($json->encode($counts));
}
